plugins {
    id 'com.android.application'
    id 'com.google.android.libraries.mapsplatform.secrets-gradle-plugin'
    id 'com.google.dagger.hilt.android'
    id 'com.google.firebase.crashlytics'
    id 'com.google.gms.google-services'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.kapt'
    id 'org.jetbrains.kotlin.plugin.parcelize'
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def storeFilePath = properties.getProperty('signing.file')
def password = properties.getProperty('signing.password')
def alias = properties.getProperty('signing.alias')
def aliasPassword = properties.getProperty('signing.alias_password')

android {
    namespace 'com.yourfitness.coach'
    compileSdk 34
    defaultConfig {
        applicationId 'com.yourfitness.coach'
        minSdk 28
        targetSdk 34
        versionCode 341
        versionName '3.4.3'
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }

        buildConfigField 'String', 'SPIKE_CALLBACK_URL', '"https://yourfitness.coach/success"'
    }
    signingConfigs {
        qa {
            keyAlias 'key0'
            keyPassword 'yfcandroid'
            storeFile file('keystore.jks')
            storePassword 'yfcandroid'
        }
        release {
            storeFile file(storeFilePath)
            storePassword password
            keyAlias alias
            keyPassword aliasPassword
        }
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.qa
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    productFlavors {
        qa {
            dimension 'environment'
            applicationIdSuffix '.qa'
            signingConfig signingConfigs.qa

            buildConfigField 'String', 'SPIKE_AUTH_TOKEN', '"71f1ed4a-81c5-4a0e-8aa7-a064a0244c29"'
            buildConfigField 'String', 'SPIKE_CLIENT_ID', '"b4baacc6-fe3e-4f2b-aeef-a02f2c37706c"'
        }
        prod {
            dimension 'environment'
            signingConfig signingConfigs.qa

            buildConfigField 'String', 'SPIKE_AUTH_TOKEN', '"622cb0cf-ac7e-41be-aa6c-39a29d7f1495"'
            buildConfigField 'String', 'SPIKE_CLIENT_ID', '"aa947af8-5a81-4c6c-8131-d4c27a0af86e"'
        }
    }
    flavorDimensions 'environment'
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
        freeCompilerArgs = ["-Xstring-concat=inline"]
    }
    kotlin {
        jvmToolchain(17)
    }
    buildFeatures {
        viewBinding true
        dataBinding true
        buildConfig = true
    }
}

dependencies {
    implementation project(':common')
    implementation project(':shop')
    implementation project(':pt')
    implementation project(':community')

    // Kotlin
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.9.0'

    // AndroidX
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.activity:activity-ktx:1.8.2'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.7.6'
    implementation 'androidx.navigation:navigation-ui-ktx:2.7.6'
    implementation 'androidx.navigation:navigation-dynamic-features-fragment:2.7.6'
    implementation 'androidx.viewpager2:viewpager2:1.0.0'
    implementation 'androidx.fragment:fragment-ktx:1.6.2'
    implementation 'androidx.paging:paging-runtime-ktx:3.2.1'

    // Google
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'com.google.android.gms:play-services-auth:20.7.0'
    implementation 'com.google.android.gms:play-services-fitness:21.1.0'
    implementation 'com.google.android.gms:play-services-maps:18.2.0'
    implementation 'com.google.android.gms:play-services-location:21.0.1'
    implementation 'com.google.maps.android:maps-ktx:3.4.0'
    implementation 'com.google.maps.android:android-maps-utils:3.4.0'
    implementation("com.google.android.play:feature-delivery:2.1.0")
    implementation("com.google.android.play:feature-delivery-ktx:2.1.0")

    //Firebase tools
    implementation platform('com.google.firebase:firebase-bom:33.1.2')
    implementation("com.google.firebase:firebase-inappmessaging-display-ktx")
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-messaging-ktx'
    implementation 'com.google.firebase:firebase-config-ktx'
    implementation 'com.google.firebase:firebase-storage-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'

    // Dagger & Hilt
    implementation 'com.google.dagger:hilt-android:2.49'
    implementation 'androidx.databinding:viewbinding:8.2.1'
    implementation 'androidx.hilt:hilt-common:1.2.0'
    implementation 'androidx.work:work-runtime-ktx:2.9.1'
    implementation 'androidx.hilt:hilt-work:1.2.0'
    implementation ("androidx.work:work-runtime:2.9.1")
    implementation 'androidx.hilt:hilt-navigation-fragment:1.2.0'
    kapt 'androidx.hilt:hilt-compiler:1.2.0'
    kapt 'com.google.dagger:hilt-android-compiler:2.49'
    kapt 'com.google.dagger:hilt-compiler:2.49'

    // Retrofit & OkHttp
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:5.0.0-alpha.14'

    // Room
    implementation 'androidx.room:room-runtime:2.6.1'
    implementation 'androidx.room:room-ktx:2.6.1'
    kapt 'androidx.room:room-compiler:2.6.1'

    // Timber
    implementation 'com.jakewharton.timber:timber:5.0.1'

    // 3rd party UI Components
    implementation 'com.github.bumptech.glide:glide:4.15.1'
    implementation 'com.vanniktech:android-image-cropper:4.5.0'
    implementation 'com.github.mukeshsolanki:country-picker-android:2.0.4'
    implementation 'com.github.shahi5472:custom-date-picker:1.1.0'
    implementation 'io.github.chaosleung:pinview:1.4.4'
    implementation 'pl.droidsonroids.gif:android-gif-drawable:1.2.25'

    // Spike SDK
    implementation "com.spikeapi.sdk:spike-sdk:3.1.6"

    // Flow Bindings
    implementation 'io.github.reactivecircus.flowbinding:flowbinding-android:1.2.0'
    implementation 'io.github.reactivecircus.flowbinding:flowbinding-material:1.2.0'

    // ViewBinding Delegate
    implementation 'com.github.kirich1409:viewbindingpropertydelegate:1.5.6'

    // StoryView
    implementation 'com.github.shts:StoriesProgressView:3.0.0'

    // Calendar
    implementation 'com.kizitonwose.calendar:view:2.3.0'

    // Circle diagram
    implementation 'com.github.antonKozyriatskyi:CircularProgressIndicator:1.3.0'

    //Payments
    implementation 'com.stripe:stripe-android:20.21.1'

    //EditText input mask
    implementation("com.redmadrobot:input-mask-android:6.1.0")

    // Testing
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.4'

    implementation("com.github.KinesteX:KinesteX-SDK-Kotlin:1.1.5")
}